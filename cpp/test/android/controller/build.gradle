//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'gradle.plugin.com.zeroc.gradle.ice-builder:slice:1.4.7'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.zeroc.gradle.ice-builder.slice'

slice {
    output = file("${project.buildDir}/generated/source/ice")
    cppConfiguration = project.cppConfiguration
    cppPlatform = project.cppPlatform
    if(!System.env.ICE_BIN_DIST?.split(" ").find{ it == 'all' || it.contains('cpp')}) {
        iceHome = project.hasProperty('iceHome') ? project.iceHome
                : System.getenv("ICE_HOME") != null ? System.env.ICE_HOME : new File("$rootProject.projectDir/../../../..").getCanonicalPath()
    }
    java {
        files = fileTree(dir: "$rootProject.projectDir/../../../../scripts/", includes: ['*.ice'])
    }
}

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.zeroc.testcontrollercpp"
        minSdkVersion 24
        targetSdkVersion 29
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    sourceSets {
        release {
            jniLibs.srcDirs '../../../gradle/lib/shared/Release'
        }
        debug {
            jniLibs.srcDirs '../../../gradle/lib/shared/Debug'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version "3.10.2"
        }
    }
}

dependencies {
    implementation "com.zeroc:ice:${project.iceVersion}"
    runtimeOnly "com.zeroc:icediscovery:${project.iceVersion}"
}
